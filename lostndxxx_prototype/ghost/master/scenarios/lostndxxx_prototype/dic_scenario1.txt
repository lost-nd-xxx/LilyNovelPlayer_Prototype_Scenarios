
＊lostndxxx_prototype.はじめから
（call,ラベル通過,はじめから,オートセーブ不可）
＃中断からの復帰はOKだがオートセーブ地点にはしたくない場合

＄s_lostndxxx_prototype.オートセーブ	|
＃はじめからなのでオートセーブのみ初期化
＃他のセーブスロットやグローバルセーブには影響しない

＄lostndxxx_prototype.クリック待ちマーカー設定	（__default.クリック待ちマーカー）
＃独自のクリック待ち設定を無効にする

（スキップ解除）
（オート解除）
＃スキップとオートを解除する

＄バックログ状態	1
＃バックログを有効に

（メニュー不可）
＃メニュー操作を許可しない

（freeimage）
＃画面描画をクリアする。
＃引数無しはレイヤ４枚すべてをクリアする

：\_q
\_l[10,10]この先、音が出るんやで。\n
\_l[10,]音量調整は今のうちにしときや。
\_q\x
（nextjump,シナリオＡ）



＊lostndxxx_prototype.シナリオＡ
（メニュー許可）
＃メニュー操作を許可する

（freeimage）

（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
＃画像を描画する　基本的には後に書いたものほど手前に来る

（音楽１）
＃dic_snippet.txtも参照のこと

：（名札,【緑０号】,20,20）\n
＃名前表示的なもの
（ボイス１）
\_l[20,]何や、\w5この光景は。
＃\_l[x座標,]で位置指定をしないと左端から文字が出る

（nextset,シナリオＡ,１）
＃次のシーンへのクリック待ちをしつつ、
＃次のシナリオ接頭辞と数値の開始値を設定する

＊lostndxxx_prototype.シナリオＡ１
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）
：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]懐かしいやろ？\w9　昔風のデスクトップやで。
（next）
＃次のシーンへのクリック待ち

＊lostndxxx_prototype.シナリオＡ２
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]もう令和やぞ。\w9似てへんし。
（next）

＊lostndxxx_prototype.シナリオＡ３
＄バックログ状態	1
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]せやったら、\w5移動しよか。
（next）

＊lostndxxx_prototype.シナリオＡ４
（メニュー不可）
（freeimage）
（移動背景１）\w9\w9
（移動背景２）\w9\w9
（移動背景３）\w9\w9
（移動背景４）\w9\w9
（nextjump,シナリオＡ４_移動完了）

＊lostndxxx_prototype.シナリオＡ４_移動完了
（メニュー許可）
（背景２）
（字枠２）
（みどうにゅ２）
（音楽２）

：（名札,【緑０号】,180,230）\n
（ボイス１）
\_l[180,]\f[height,36]高っ…\w3…\w3！！？？
＃フォントサイズ変え　色変えなども同じ要領で
＃色を戻す時は（文字色.current）を入れる
＃\f[default]を入れると設定中バルーンのデフォルトに戻ってしまう
（nextset,シナリオＡ,５）

＊lostndxxx_prototype.シナリオＡ５
（メニュー許可）
（背景２）
（字枠２）
（おれうにゅ２）
（音楽２）

：（名札,【橙１号】,180,230）\n
（ボイス２）
\_l[180,]メッセージウィンドウに\n
\_l[180,]顔が入るタイプもええよな。
（next）

＊lostndxxx_prototype.シナリオＡ６
（メニュー許可）
（背景２）
（字枠２）
（みどうにゅ３）
（音楽２）

：（名札,【緑０号】,180,230）\n
（ボイス１）
\_l[180,]\f[height,36]ええから、\w5降ろして。
（next,緑０号好感度,0）
＃この後に好感度を使うので初期値を入れておく

＊lostndxxx_prototype.シナリオＡ７
（メニュー許可）
（背景２）
（字枠２）
（おれうにゅ２）
（音楽２）

：（名札,【橙１号】,180,230）\n
（ボイス２）
\_l[180,]しゃーないな…\w3…\n[150]

\_l[180,]\![*]（選択肢,【降ろす】,シナリオＢ,緑０号好感度,（calc,（変数読み出し,緑０号好感度）+10））\n
\_l[180,]\![*]（選択肢,【降ろさない】,シナリオＣ,緑０号好感度,（calc,（変数読み出し,緑０号好感度）-10））
＃選択肢を選んだ後に変数を操作する



＊lostndxxx_prototype.シナリオＢ
（メニュー許可）
（freeimage）
（背景１）
（みどうにゅ４）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]怖かった…\w3…\w3。
（nextset,シナリオＢ,１）

＊lostndxxx_prototype.シナリオＢ１
（メニュー許可）
（背景１）
（freeimage,2）
（おれうにゅ３）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]ガチ恋距離やめい。
（next）

＊lostndxxx_prototype.シナリオＢ２
（メニュー許可）
（背景１）
（freeimage,2）
（みどうにゅ４）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]現在の緑０号の好感度は（変数読み出し,緑０号好感度）やで。
＃ゲーム内変数を読んで表示する
（when,（変数読み出し,緑０号好感度）>0,（nextset,シナリオＤ,１）,（next））
＃「緑０号好感度」によって分岐する

＊lostndxxx_prototype.シナリオＢ３
＃シナリオＢ２の時に緑０号好感度が０以下だとここに来る
（メニュー許可）
（背景１）
（freeimage,2）
（字枠１）
（音楽１）

：\_l[0,0]\n[200]
\f[align,center]～おしまい～
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]

＃以下のような形でタイトルに戻ると文字色設定がバルーンのデフォルトになってしまう
＃おしまい\x
＃＞lostndxxx_prototype.起動



＊lostndxxx_prototype.シナリオＣ
（メニュー許可）
（freeimage）
（背景２）
（みどうにゅ４）
（字枠１）
（音楽２）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]ぴえん。\w9\n
\_l[20,]現在の緑０号の好感度は（変数読み出し,緑０号好感度）やで。
（when,（変数読み出し,緑０号好感度）>0,（nextset,シナリオＤ,１）,（nextset,シナリオＣ,１））
＃「緑０号好感度」によって分岐する

＊lostndxxx_prototype.シナリオＣ１
＃シナリオＣの時に緑０号好感度が０以下だとここに来る
（メニュー許可）
（背景２）
（freeimage,2）
（字枠１）
（音楽２）

：\_l[0,0]\n[200]
\f[align,center]～おしまい～
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]



＊lostndxxx_prototype.シナリオＤ１
＄サーフェス_ウィンドウ	2
＃タイトルバーを\s[2]へ変える
（メニュー許可）
（freeimage）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]緑０号好感度が０より大きいと、\w5ここに来るで。

（next,__global__タイトルバー,特殊）

＊lostndxxx_prototype.シナリオＤ２
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：\_l[0,0]\n[200]
\f[align,center]～おしまい～
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]
