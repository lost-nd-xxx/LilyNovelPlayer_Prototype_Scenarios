
＊lostndxxx_prototype.起動
＃最初に来るのは必ず「＊【シェルフォルダ名】.起動」

＄__tmp	（call,ラベル通過,起動,オートセーブ不可）
＃オートセーブはさせたくないが中断からの復帰はしてよい

＄文字色.current	（文字色.normal）
＃文字色を普通の色に設定する

＄文字色強制	
＃設定を解除しておく

\![raise,lostndxxx_prototype.タイトル]
＃文字色を反映させるため、いったん区切り



＊lostndxxx_prototype.タイトル
＄サーフェス_ウィンドウ	0
＃タイトルバーを\s[0]へ変える

＄__tmp	（変数読み出しg,,タイトルバー）
＄サーフェス_ウィンドウ	（when,（変数「__tmp」の存在）==1&&（compare,（__tmp）,特殊）==1,2,0）
＃グローバル変数にタイトルバーを特殊にするフラグがあればウィンドウを\s[2]にする

＄__tmp	
＄lostndxxx_prototype.クリック待ちマーカー設定	
＃不要な変数を消しておく

＄バックログ状態	0
＃バックログを有効にするかどうか。1で有効

（バックログ全削除）
（スキップ解除）
（オート解除）
＃スキップとオートを解除

（メニュー不可）
＃メニュー系の操作を全部不可にする

＄メニュー状態.コンフィグ	1
＄メニュー状態.ロード	1
＃許可していいものだけ許可する

（freeimage）
＃それまでの描画状態をクリアする
＃引数無しはレイヤ４枚すべてをクリアする

（背景３）
＃タイトル画面の背景　dic_snippet.txtも参照のこと

（音停止）
＃すべての音を止める

\![close,inputbox,__SYSTEM_ALL_INPUT__]
＃インプットボックスが出ていたら消しておく

：\f[shadowcolor,#333]\f[shadowstyle,outline]
＃文字に黒の縁取りをつける

\_q\n[150]
\f[height,48]\f[align,center]りりぷれ\nぷろとたいぷ

\f[height,default]\*
\_l[0,0]\n[700]
＃位置合わせ

（nop,（split,（s_（ゲームID）.オートセーブ）,|,0,0））
＃オートセーブから再開できるかどうかの下調べ
（when、（文「（ゲームID）.（S0）」の存在）==1、
\_l[50,]\__q[OnResumeGame,オートセーブ]つづきから\__q\n
＃オートセーブから再開できるならさせる
）

\_l[50,]\__q[メニュー.ロード]ロード\__q\n
＃セーブデータを選んでロード

\_l[50,]\__q[メニュー.コンフィグ]コンフィグ\__q\n
＃コンフィグ画面へ飛ばす

\_l[50,]\__q[メニュー.終了ダイアログ]おわる\__q
＃終了するかどうか聞く

\_l[0,0]\n[700]
＃位置合わせ

\_l[250,]\__q[lostndxxx_prototype.はじめから]シナリオ１をはじめから\__q\n
＃「lostndxxx_prototype.チュートリアルはじめから」へ飛ばす

\_l[250,]\__q[lostndxxx_prototype.チュートリアルはじめから]シナリオ２をはじめから\__q\n[200]
＃「lostndxxx_prototype.チュートリアルはじめから」へ飛ばす

\_q


