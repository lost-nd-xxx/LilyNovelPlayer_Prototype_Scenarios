
＊lostndxxx_prototype.チュートリアルはじめから
（call,ラベル通過,チュートリアルはじめから,オートセーブ不可）
＃中断からの復帰はOKだがオートセーブ地点にはしたくない場合

＄s_lostndxxx_prototype.オートセーブ	|
＃はじめからなのでオートセーブのみ初期化
＃他のセーブスロットやグローバルセーブには影響しない

＄lostndxxx_prototype.クリック待ちマーカー設定	（__default.クリック待ちマーカー）
＃独自のクリック待ち設定を無効にする

（スキップ解除）
（オート解除）
＃スキップとオートを解除する

＄バックログ状態	1
＃バックログを有効に

（メニュー不可）
＃メニュー操作を許可しない

（freeimage）
＃画面描画をクリアする。
＃引数無しはレイヤ４枚すべてをクリアする

：\_q
\_l[10,10]この先、音が出るんやで。\n
\_l[10,]音量調整は今のうちにしときや。
\_q\x
（nextjump,チュートリアルＡ０）



＊lostndxxx_prototype.チュートリアルＡ０
（メニュー許可）
＃メニュー操作を許可する

（背景明）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
＃画像を描画する
＃dic_snippet.txtも参照のこと

：（名札,【緑０号】,20,20）\n
＃名前表示的なもの
（ボイス１）
\_l[20,]まいど。\w9\n
（ボイス１）
\_l[20,]このゴーストは\w3\f[bold,1]Lily Novel Player\f[bold,0]\w3っちゅうんや。\w9\n
（ボイス１）
\_l[20,]ワイら共々、\w5よろしゅうな。
＃\_l[x座標,]で位置指定をしないと左端から文字が出る
（nextset,チュートリアルＡ,１）
＃次のシーンへのクリック待ちをしつつ、
＃次のシナリオ接頭辞と数値の開始値を設定する

＊lostndxxx_prototype.チュートリアルＡ１
（メニュー許可）
（背景明）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
＃メニュー許可状態・画像・音楽は、nextや選択肢での移動後、毎回指定する必要がある
＃変化がなければ再描画はされない
＃セーブデータからのロード時のための措置

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]このゴースト…\w5…\w5略して\f[bold,1]LNP\f[bold,0]を使えば、\w5\n
（ボイス１）
\_l[20,]SSPで動作する\f[bold,1]ノベルゲームを作れる\f[bold,0]んや。\w9\n
（ボイス１）
\_l[20,]SHIORIは里々やで。
（next）
＃次のシーンへのクリック待ち

＊lostndxxx_prototype.チュートリアルＡ２
（メニュー許可）
（背景明）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]しかし、\w5ゲームっちゅうんなら、\w5\n
（ボイス２）
\_l[20,]背景と音楽が欲しいわな。
（next）

＊lostndxxx_prototype.チュートリアルＡ３
（メニュー許可）
（背景明）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]もちろんあるで。\w9ちょっと待ちや。
（next）

＊lostndxxx_prototype.チュートリアルＡ４
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]こんな感じやで。\w9\n
（ボイス１）
\_l[20,]タグ辞書はタイトル画面から開けるさかい、\w5\n
（ボイス１）
\_l[20,]後で見てみてな。
（next）

＊lostndxxx_prototype.チュートリアルＡ５
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]おっ、\w5ええな。\w9\n
（ボイス２）
\_l[20,]画像の切り替え効果みたいなんは無いんか？
（next）

＊lostndxxx_prototype.チュートリアルＡ６
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]無いで。\w9\n
（ボイス１）
\_l[20,]せやけど、\w5工夫次第で多少は何とかなるかもしらん。
（next）

＊lostndxxx_prototype.チュートリアルＡ７
（メニュー不可）
（freeimage）
（移動背景１）\w9\w9
（移動背景２）\w9\w9
（移動背景３）\w9\w9
（移動背景４）\w9\w9
（nextjump,チュートリアルＡ８）

＊lostndxxx_prototype.チュートリアルＡ８
（メニュー許可）
（背景２）
（字枠２）
（みどうにゅ３）
（音楽２）

：（名札,【緑０号】,180,230）\n
（ボイス１）
\_l[180,]…\w5…\w5とまあ、\w5こんな感じに。
（next）

＊lostndxxx_prototype.チュートリアルＡ９
（メニュー許可）
（背景２）
（字枠２）
（おれうにゅ２）
（音楽２）

：（名札,【橙１号】,180,230）\n
（ボイス２）
\_l[180,]メッセージウィンドウに\n
（ボイス２）
\_l[180,]顔が入るタイプか。\w9\n
（ボイス２）
\_l[180,]これもええな。
（next）

＊lostndxxx_prototype.チュートリアルＡ１０
（メニュー許可）
（freeimage,1）
（背景２）
（みどうにゅ５）
（おれうにゅ４）
（字枠３）
（音楽２）

： 
＃ボイスの前に何か表示しておかないとレイヤの表示順が壊れる
（ボイス１）
\_l[20,30]\f[color,#a5e12d]「ノベルゲーム言うたら、\w5この形式も要るよな」\w9\n[150]
\_l[20,]（文字色.current）　緑０号はドヤ顔をキめた。\w9\n[150]
（ボイス２）
\_l[20,]\f[color,#fb961f]「ワイも同意するが…\w5…\w5、\w5その顔、\w5ウザいで」\w9\n[150]
（ボイス１）
\_l[20,]\f[color,#a5e12d]\f[height,150%]「見えとらんやろ」\w9\f[height,default]\n[150]
（ボイス２）
\_l[20,]\f[color,#fb961f]「見えんでも、\w5わかるときはある…\w5…」\w9\n[150]
（ボイス１）
\_l[20,]\f[color,#a5e12d]「…\w5…\w5ちなみに、\w5バックログ機能もあるで」
（文字色.current）
（next）
＃フォントサイズ変え　色変えなども同じ要領で
＃色を戻す時は（文字色.current）を入れる
＃\f[default]を入れると設定中バルーンのデフォルトに戻ってしまう

＊lostndxxx_prototype.チュートリアルＡ１１
（メニュー許可）
（freeimage）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]あとは選択肢やね。\w9好きな位置に出せるで。\n[150]
\_l[20,]\![*]（選択肢,メッセージウインドウに出したり,チュートリアルＢ０,チュートリアル分岐,上）\n

（選択肢、\_l[0,195]\f[align,center]\_b["assets/preset/choice_base1.png",inline,--option=use_self_alpha,--option=foreground]\_l[0,210]\f[align,center]画像を敷いてボタンっぽくしたり、チュートリアルＢ０、チュートリアル分岐、下）



＊lostndxxx_prototype.チュートリアルＢ０
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]なるほどなー。\w9\n
（ボイス２）
\_l[20,]分岐したりもできるんか？
（nextset,チュートリアルＢ,１）

＊lostndxxx_prototype.チュートリアルＢ１
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]もちろんやで。\w9\n
（ボイス１）
\_l[20,]さっき選択肢を押したとき、\w5\n
（ボイス１）
\_l[20,]どっち押したか覚えとるか？
（next）

＊lostndxxx_prototype.チュートリアルＢ２
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【橙１号】,20,20）\n
（ボイス２）
\_l[20,]（変数読み出し、チュートリアル分岐）やろ。\w9ちゃんとわかるわ。
（next）

＊lostndxxx_prototype.チュートリアルＢ３
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【緑０号】,20,20）\n
（ボイス１）
\_l[20,]じゃあ最後に、\w5タイトルバーをいじってみるで。\w9\n
（ボイス１）
\_l[20,]せっかくやから、\w5分岐してみよか。\n[150]
\_l[20,]\![*]（選択肢,タイトルバーを特殊にする,チュートリアルＣ１,__global__タイトルバー,特殊）\n
\_l[20,]\![*]（選択肢,タイトルバーを通常にする,チュートリアルＣ２,__global__タイトルバー,通常）

＊lostndxxx_prototype.チュートリアルＣ１
＄サーフェス_ウィンドウ	2
＃タイトルバーを\s[2]へ変える
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【二体のうにゅう】,20,20）\n
（ボイス同時）
\_l[20,]ほな、\w5おおきに。\w9\n[150]
\f[align,center]～おしまい～
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]

＊lostndxxx_prototype.チュートリアルＣ２
＄サーフェス_ウィンドウ	0
＃ウィンドウを\s[0](通常色)にする
（メニュー許可）
（背景１）
（みどうにゅ１）
（おれうにゅ１）
（字枠１）
（音楽１）

：（名札,【二体のうにゅう】,20,20）\n
（ボイス同時）
\_l[20,]ほな、\w5おおきに。\w9\n[150]
\f[align,center]～おしまい～
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]


